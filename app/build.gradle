apply plugin: 'com.android.application'

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    compileSdkVersion 31

    splits {
        // https://developer.android.com/reference/tools/gradle-api/7.0/com/android/build/api/dsl/Splits
        // this reduces the apk size by about 2 to 3 MB
        abi {
            enable true
            reset()
            include "arm64-v8a", "armeabi-v7a", "x86", "x86_64"
            universalApk false
        }
    }

    defaultConfig {
        // https://developer.android.com/reference/tools/gradle-api/7.0/com/android/build/api/dsl/DefaultConfig
        applicationId "de.freehamburger"
        minSdkVersion 21
        //noinspection OldTargetApi
        targetSdkVersion 31
        versionCode 5
        versionName "1.3"
        resConfigs 'en', 'de'
        buildConfigField("long", "BUILD_TIME", String.valueOf(System.currentTimeMillis()) + "L")
        testApplicationId applicationId + ".test"
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        testInstrumentationRunnerArguments disableAnalytics: 'true'
    }

    signingConfigs {
        // https://developer.android.com/reference/tools/gradle-api/7.0/com/android/build/api/dsl/SigningConfig
        release {
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
        }
    }

    buildTypes {
        // https://developer.android.com/reference/tools/gradle-api/7.0/com/android/build/api/dsl/BuildType
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
            manifestPlaceholders = [name: "@string/app_name_debug", icon: "@mipmap/ic_launcher_debug", roundicon: "@mipmap/ic_launcher_round_debug"]
            resValue "string", "app_search_action", defaultConfig.applicationId + applicationIdSuffix + '.ACTION_SEARCH'
            resValue "string", "app_search_auth", defaultConfig.applicationId + applicationIdSuffix + '.SearchContentProvider'
            resValue "string", "app_search_sid", "content://" + defaultConfig.applicationId + applicationIdSuffix + '/sid'
            resValue "string", "main_action_font_delete", defaultConfig.applicationId + applicationIdSuffix + '.action.font_delete'
            resValue "string", "main_action_font_import", defaultConfig.applicationId + applicationIdSuffix + '.action.font_import'
            crunchPngs true
            testCoverageEnabled = false
        }
        release {
            manifestPlaceholders = [name: "@string/app_name", icon: "@mipmap/ic_launcher", roundicon: "@mipmap/ic_launcher_round"]
            resValue "string", "app_search_action", defaultConfig.applicationId + '.ACTION_SEARCH'
            resValue "string", "app_search_auth", defaultConfig.applicationId + '.SearchContentProvider'
            resValue "string", "app_search_sid", "content://" + defaultConfig.applicationId + '/sid'
            resValue "string", "main_action_font_delete", defaultConfig.applicationId + '.action.font_delete'
            resValue "string", "main_action_font_import", defaultConfig.applicationId + '.action.font_import'
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            shrinkResources true
            crunchPngs true
            signingConfig signingConfigs.release
        }
    }

    compileOptions {
        // https://developer.android.com/reference/tools/gradle-api/7.0/com/android/build/api/dsl/CompileOptions
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lint {
        // UseCompatLoadingForDrawables is misleading for minSdkVersion >= 21
        ignore 'UseCompatLoadingForDrawables'
    }
    namespace 'de.freehamburger'

}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.4.2'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'androidx.preference:preference:1.2.0'
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.1.0"
    // https://github.com/material-components/material-components-android
    implementation 'com.google.android.material:material:1.6.1'
    // https://github.com/square/okhttp
    //noinspection GradleDependency
    implementation 'com.squareup.okhttp3:okhttp:4.10.0'
    // https://github.com/google/conscrypt - integrates with okhttp (see https://github.com/square/okhttp) - this is optional; the app runs without it
    implementation 'org.conscrypt:conscrypt-android:2.5.2'
    //noinspection GradleDependency
    implementation 'com.squareup.picasso:picasso:2.8'
    // https://github.com/google/ExoPlayer
    implementation 'com.google.android.exoplayer:exoplayer-core:2.17.1'
    implementation 'com.google.android.exoplayer:exoplayer-ui:2.17.1'
    implementation 'com.google.android.exoplayer:exoplayer-hls:2.17.1'
    implementation 'com.google.android.exoplayer:extension-okhttp:2.17.1'

    implementation 'com.github.chrisbanes:PhotoView:2.3.0'

    // https://developer.android.com/jetpack/androidx/releases/test
    androidTestImplementation 'androidx.test:core:1.5.0'
    androidTestImplementation 'androidx.test:runner:1.5.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.0'
    androidTestImplementation 'androidx.test:rules:1.5.0'

    testImplementation 'androidx.test:runner:1.5.1'
}
