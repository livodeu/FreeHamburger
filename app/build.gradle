apply plugin: 'com.android.application'

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "de.freehamburger"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 1
        versionName "0.1"
        resConfigs "en", "de"
        buildConfigField("long", "BUILD_TIME", String.valueOf(System.currentTimeMillis()) + "L")
        testApplicationId "de.freehamburger.test"
        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
        testInstrumentationRunnerArguments disableAnalytics: 'true'
    }

    signingConfigs {
        release {
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
        }
    }

    dexOptions {
        maxProcessCount 8
        javaMaxHeapSize "2g"
        preDexLibraries true
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
            manifestPlaceholders = [name: "@string/app_name_debug", icon:"@mipmap/ic_launcher_debug", roundicon: "@mipmap/ic_launcher_round_debug"]
            minifyEnabled false
            useProguard false
            crunchPngs true
            aaptOptions {
                cruncherEnabled true
            }
            testCoverageEnabled = true
        }
        release {
            manifestPlaceholders = [name: "@string/app_name", icon:"@mipmap/ic_launcher", roundicon: "@mipmap/ic_launcher_round"]
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            useProguard true
            crunchPngs true
            aaptOptions {
                cruncherEnabled true
            }
            signingConfig signingConfigs.release
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support:support-v4:28.0.0'
    implementation 'com.android.support:design:28.0.0'
    implementation 'com.android.support:recyclerview-v7:28.0.0'
    implementation 'com.android.support:exifinterface:28.0.0'           // WTF: without this there is an error in "implementation 'com.android.support:appcompat-v7:28.0.0'"
    //noinspection GradleDependency
    implementation 'com.squareup.okhttp3:okhttp:3.14.2'                 // also working: 4.0.0 3.14.1 3.12.1 3.11.0
    implementation 'com.squareup.picasso:picasso:2.71828'
    //noinspection GradleDependency
    implementation 'com.google.android.exoplayer:exoplayer-core:2.9.6'  // also working: 2.9.[5..1]; not working: 2.10.0
    //noinspection GradleDependency
    implementation 'com.google.android.exoplayer:exoplayer-ui:2.9.6'    // also working: 2.9.[5..0]; not working: 2.10.0
    //noinspection GradleDependency
    implementation 'com.google.android.exoplayer:exoplayer-hls:2.9.6'   // also working: 2.9.[5..0]; not working: 2.10.0

    androidTestImplementation 'com.android.support:support-annotations:28.0.0'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
}
