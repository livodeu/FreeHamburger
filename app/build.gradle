apply plugin: 'com.android.application'

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    compileSdkVersion 31

    splits {
        // this reduces the apk size by about 2 to 3 MB
        abi {
            enable true
            reset()
            include "arm64-v8a", "armeabi-v7a", "x86", "x86_64"
            universalApk false
        }
    }

    defaultConfig {
        applicationId "de.freehamburger"
        minSdkVersion 21
        targetSdkVersion 31
        versionCode 3
        versionName "1.0.2"
        resConfigs "en", "de"
        buildConfigField("long", "BUILD_TIME", String.valueOf(System.currentTimeMillis()) + "L")
        testApplicationId applicationId + ".test"
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        testInstrumentationRunnerArguments disableAnalytics: 'true'
    }

    signingConfigs {
        release {
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
        }
    }

    lintOptions {
        // UseCompatLoadingForDrawables is misleading for minSdkVersion >= 21
        ignore("UseCompatLoadingForDrawables")
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
            manifestPlaceholders = [name: "@string/app_name_debug", icon: "@mipmap/ic_launcher_debug", roundicon: "@mipmap/ic_launcher_round_debug"]
            resValue "string", "app_search_auth", defaultConfig.applicationId + applicationIdSuffix + '.SearchContentProvider'
            crunchPngs true
            testCoverageEnabled = false
        }
        release {
            manifestPlaceholders = [name: "@string/app_name", icon: "@mipmap/ic_launcher", roundicon: "@mipmap/ic_launcher_round"]
            resValue "string", "app_search_auth", defaultConfig.applicationId + '.SearchContentProvider'
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            shrinkResources true
            crunchPngs true
            signingConfig signingConfigs.release
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'androidx.preference:preference:1.1.1'
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.1.0"
    // https://github.com/material-components/material-components-android
    implementation 'com.google.android.material:material:1.4.0'
    // https://github.com/square/okhttp
    //noinspection GradleDependency
    implementation 'com.squareup.okhttp3:okhttp:4.9.1'
    // https://github.com/google/conscrypt - integrates with okhttp (see https://github.com/square/okhttp) - this is optional; the app runs without it
    implementation 'org.conscrypt:conscrypt-android:2.5.2'
    //noinspection GradleDependency
    implementation 'com.squareup.picasso:picasso:2.8'
    // https://github.com/google/ExoPlayer
    implementation 'com.google.android.exoplayer:exoplayer-core:2.12.3'
    implementation 'com.google.android.exoplayer:exoplayer-ui:2.12.3'
    implementation 'com.google.android.exoplayer:exoplayer-hls:2.12.3'
    implementation 'com.google.android.exoplayer:extension-okhttp:2.12.3'

    androidTestImplementation 'androidx.test:core:1.4.0'
    androidTestImplementation 'androidx.test:runner:1.4.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    androidTestImplementation 'androidx.test:rules:1.4.0'
}
